plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'androidx.navigation.safeargs.kotlin'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    Properties properties = new Properties()
    if (rootProject.file("local.properties").exists()) {
        properties.load(rootProject.file("local.properties").newDataInputStream())
    }

    signingConfigs {
        debug {
            storeFile file('..\\keystore\\appKeyNbaDebug.jks')
            storePassword 'appKey'
            keyAlias 'appKey'
            keyPassword 'appKey'
        }
        release {
            storeFile file(properties.getProperty('keyStore.release.location'))
            storePassword properties.getProperty('keyStore.release.storePassword')
            keyAlias properties.getProperty('keyStore.release.keyAlias')
            keyPassword properties.getProperty('keyStore.release.keyPassword')
        }
    }

    defaultConfig {
        applicationId "com.hongwei.android_nba_assist"
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        resValue "string", "public_access_token", properties.getProperty("publicAccess.token", "")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += [
                "-P",
                "plugin:androidx.compose.compiler.plugins.kotlin:suppressKotlinVersionCompatibilityCheck=true"
        ]
    }
    buildFeatures {
        compose true
        viewBinding true
        composeOptions {
            kotlinCompilerExtensionVersion "1.0.0-beta01"
//            kotlinCompilerVersion '$kotlin_version'
        }
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.firebase:firebase-messaging:21.1.0'
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"

    implementation('com.squareup.okhttp3:okhttp:5.0.0-alpha.2')
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    implementation('com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2')
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'
    implementation('com.squareup.moshi:moshi:1.12.0')
    implementation('com.squareup.moshi:moshi-kotlin:1.12.0')

    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-dynamic-features-fragment:$nav_version"
    implementation "androidx.navigation:navigation-compose:1.0.0-alpha10"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.activity:activity-compose:1.3.0-alpha07"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha04"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime-rxjava2:$compose_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha01'

    implementation 'com.google.android.material:material:1.3.0'

    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.google.accompanist:accompanist-coil:0.8.1"

    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation 'com.google.code.gson:gson:2.8.6'

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    implementation 'com.airbnb.android:lottie:3.7.0'
    implementation 'com.airbnb.android:lottie-compose:1.0.0-beta03-1'

    implementation platform('com.google.firebase:firebase-bom:27.1.0')
    implementation 'com.google.firebase:firebase-analytics-ktx:18.0.3'


    // optional - Test helpers
    testImplementation "androidx.room:room-testing:$room_version"

    testImplementation 'junit:junit:4.13.2'
    testImplementation('com.squareup.okhttp3:mockwebserver:5.0.0-alpha.2')
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    androidTestImplementation "androidx.navigation:navigation-testing:$nav_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
}